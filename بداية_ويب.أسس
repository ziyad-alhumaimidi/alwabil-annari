اشمل "مـتم/ريـاضيات"؛
اشمل "مـتم/سندات"؛
اشمل "مـتم/طـرفية"؛
اشمل "مـتم/نـظام"؛
اشمل "مـتم/نـص"؛
اشمل "مـحا.أسس"؛
مـحا.اشمل_ملف("Alusus/WebPlatform"، "مـنصة_ويب.أسس")؛
مـحا.اشمل_ملف("Alusus/Promises"، "مـؤجلات.أسس")؛

اشمل "خدمات_ويب/تـحكم.أسس"؛
اشمل "خدمات_ويب/دمـغة.أسس"؛
اشمل "خدمات_ويب/كـتابة.أسس"؛
اشمل "خدمات_ويب/صـوت.أسس"؛
اشمل "خدمات_ويب/مـوسيقى.أسس"؛

اشمل "القائمة.أسس"؛

استخدم مـتم؛
استخدم مـنصة_ويب؛

//=============================================================================
// تعريفات عمومية

عرف المرسم: سـندنا[مـرسم]؛


//=============================================================================
// أصناف

صنف ربـاعية {
    عرف أعلى: عائم = 0؛
    عرف يمين: عائم = 0؛
    عرف أسفل: عائم = 0؛
    عرف يسار: عائم = 0؛
}


//=============================================================================
// الخادم

@مسار_موارد عرف مسار_الصور: "رسوم/"؛
@مسار_موارد عرف مسار_الأصوات: "أصوات/"؛

//=============================================================================
// الصفحة

@منفذ_مرئي["/"، "الوابل الناري"]
دالة رئيسي {
    انتظر(حمل_خط("AlususMono", "رسوم/AlususMono.otf"))؛

    // تحجيز النافذة
    نـافذة.النموذج.حدد_المشهد(صـندوق().{
        الطراز.المحاذاة = مـحاذاة._وسط_؛
        أضف_فروع({
            مـرسم().{
                المرسم = هذا؛
                الطراز.{
                    العرض = مـسافة.بكسلات(1280)؛
                    الطول = مـسافة.بكسلات(720)؛
                    عرض_الصورة = مـسافة.بكسلات(1280)؛
                    طول_الصورة = مـسافة.بكسلات(720)؛
                    سمك_الإطار = مـسافة4.نقاط(2)؛
                    لون_الإطار = لـون(0, 0, 0)؛
                    الخلفية = خـلفية(لـون(220, 220, 220))؛
                }؛
            }
        })؛
    })؛

    عرف المؤشر_محتكر: ثنائي = 0؛
    عرف ملء_الشاشة: ثـنائي = 0؛
    عرف الموسيقى_مشغلة: ثـنائي = 0؛

    تحكم1.المقبض = 0؛
    تحكم2.المقبض = 1؛

    نـافذة.النموذج.{
        المفاتيح_المكبوتة = "ArrowUp,ArrowDown,ArrowLeft,ArrowRight,Space"؛
        عند_بدء_الكبسة.اربط(مغلفة (
            سص: كسند، المؤشر_محتكر: كسند، ملء_الشاشة: كسند، الموسيقى_مشغلة: كسند، موسيقى: كسند
        )&(
            نافذة: سند[نـافذة]، حمولة: سند[نـص]
        ) {
            إذا ليس الموسيقى_مشغلة {
                شغل_الموسيقى()؛
                الموسيقى_مشغلة = 1؛
            }

            إذا حمولة == "ArrowUp" تحكم1.حالة_أزرار_الاتجاهات.أعلى = -1
            وإلا إذا حمولة == "ArrowDown" تحكم1.حالة_أزرار_الاتجاهات.أسفل = 1
            وإلا إذا حمولة == "ArrowRight" تحكم1.حالة_أزرار_الاتجاهات.يمين = 1
            وإلا إذا حمولة == "ArrowLeft" تحكم1.حالة_أزرار_الاتجاهات.يسار = -1
            وإلا إذا حمولة == "Space" تحكم1.حالة_زر_الفعل = 1
            وإلا إذا حمولة == "Enter" {
                إذا ملء_الشاشة ألغ_ملء_الشاشة()
                وإلا المرسم.التمس_ملء_الشاشة()؛
            }
        })؛
        عند_انتهاء_الكبسة.اربط(مغلفة (سص: كسند)&(نافذة: سند[نـافذة]، حمولة: سند[نـص]) {
            إذا حمولة == "ArrowUp" تحكم1.حالة_أزرار_الاتجاهات.أعلى = 0
            وإلا إذا حمولة == "ArrowDown" تحكم1.حالة_أزرار_الاتجاهات.أسفل = 0
            وإلا إذا حمولة == "ArrowRight" تحكم1.حالة_أزرار_الاتجاهات.يمين = 0
            وإلا إذا حمولة == "ArrowLeft" تحكم1.حالة_أزرار_الاتجاهات.يسار = 0
            وإلا إذا حمولة == "Space" تحكم1.حالة_زر_الفعل = 0
        })؛
        عند_تغير_احتكار_المؤشر.اربط(مغلفة (المؤشر_محتكر: كسند)&(نافذة: سند[نـافذة]، حمولة: سند[ثـنائي]) {
            المؤشر_محتكر = حمولة؛
        })؛
        عند_تغير_ملء_الشاشة.اربط(مغلفة (ملء_الشاشة: كسند)&(نافذة: سند[نـافذة]، حمولة: سند[ثـنائي]) {
            ملء_الشاشة = حمولة؛
            إذا حمولة المرسم.التمس_احتكار_المؤشر()
            وإلا ألغ_احتكار_المؤشر()؛
        })؛
        عند_ربط_مقبض.اربط(مغلفة (الموسيقى_مشغلة: كسند)&(نافذة: سند[نـافذة]، حمولة: سند[نـص]) {
            تـحكم.عدد_المقابض = هات_عدد_المقابض()؛
            إذا ليس الموسيقى_مشغلة {
                شغل_الموسيقى()؛
                الموسيقى_مشغلة = 1؛
            }
        })؛
        عند_فصل_مقبض.اربط(مغلفة (نافذة: سند[نـافذة]، حمولة: سند[نـص]) {
            تـحكم.عدد_المقابض = هات_عدد_المقابض()؛
        })؛
    }؛

    جهز_الموارد()؛
    القائمة()؛
}

//=============================================================================
// تشغيل الخادم

طـرفية.اطبع("تشغيل الخادم على المنفذ 8010...\nURL: http://localhost:8010/\n")؛
شغل_الخادم ({ "listening_ports"، "8010"، "static_file_max_age"، "0" })؛

