اشمل "اللعبة.أسس"؛

//=============================================================================
// تعريفات عمومية

عرف تحكم1: تـحكم؛
عرف تحكم2: تـحكم؛
عرف زوجي :ثنائي؛
عرف موسيقى: مـوسيقى؛
عرف نقش_القائمة: دمـغة؛
عرف نقش_مؤشر_القائمة: دمـغة؛

//=============================================================================
// دالات

دالة جهز_الموارد {
    انتظر(مـؤجلات.مـؤجلة[صحيح].الكل({
        موسيقى.حمل("أصوات/BoxCat_Games_-_07_-_Inspiration.mp3")،
        نقش_القائمة.حمل("رسوم/قائمة_البداية_بلا_خروج.png")،
        نقش_مؤشر_القائمة.حمل("رسوم/طائرة.png")
    }).تجاهل_النتيجة())؛
    جهز_موارد_اللعبة()؛
}

دالة شغل_الموسيقى {
    موسيقى.شغل()؛
}

دالة القائمة {
    عرف صخ: صحيح = 300؛
    بينما 1 {
        نقش_القائمة.اطمغ(0، 0)؛
        نقش_مؤشر_القائمة.اطمغ(180، صخ+30)؛
        إذا تحكم1.المحور_ص_السابق < 0.5 و تحكم1.المحور_ص >= 0.5 و صخ < 400 { صخ += 100 }؛
        إذا تحكم1.المحور_ص_السابق > -0.5 و تحكم1.المحور_ص <= -0.5 و صخ > 300 { صخ -= 100 }؛
        إذا تحكم1.زر_الفعل_السابق == 0 و تحكم1.زر_الفعل == 1 و صخ == 300 {
            زوجي = 1؛
            هيئ_اللعبة()؛
            اللعبة()؛
        }؛
        إذا تحكم1.زر_الفعل_السابق == 0 و تحكم1.زر_الفعل == 1 و صخ == 400 {
            زوجي = 0؛
            هيئ_اللعبة()؛
            ل1.صحة = 3؛
            ل1.ميت = 1؛
            ل1.عداد_الانفجار = 300؛
            اللعبة()؛
        }؛

        تحكم1.حدث()؛
        تحكم2.حدث()؛
        مـتم.نـظام.نم(15000)؛
        عالج_الأحداث()؛
    }
}

