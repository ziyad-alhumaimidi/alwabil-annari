صنف دمـغة {
    عرف نقش: مؤشر[طـوب.نـقش] = 0؛
    عرف أبعاد: أبـعاد؛
    عرف شفافية: عائم = 1؛

    عملية هذا~هيئ () {
    }

    عملية هذا~هيئ (سند[دمـغة]) {
        هذا.نقش = قيمة.نقش؛
        هذا.أبعاد = قيمة.أبعاد؛
        هذا.شفافية = قيمة.شفافية؛
    }

    عملية هذا~أتلف() {
        هذا.حرر()؛
    }

    عملية هذا=سند[دمـغة] {
        هذا.نقش = قيمة.نقش؛
        هذا.أبعاد = قيمة.أبعاد؛
        هذا.شفافية = قيمة.شفافية؛
    }

    عملية هذا.هيئ(ن: مؤشر[طـوب.نـقش]) {
        هذا.حرر()؛
        هذا.نقش = ن؛
        طـوب.نـقش.استفهم(هذا.نقش، 0، 0، هذا.أبعاد.عرض~مؤشر، هذا.أبعاد.طول~مؤشر)؛
    }

    عملية هذا.حمل(اسم_الملف: مـؤشر_محارف): سـندنا[مـؤجلات.مـؤجلة[صحيح]] {
        هذا.حرر()؛
        هذا.نقش = طـوب.صـورة.حمل_نقش(رسام، اسم_الملف)؛
        إذا هذا.نقش == 0 {
            طـرفية.اطبع(نـص("فشل تحميل النقش: ") + اسم_الملف)؛
            اخرج(1)؛
        }
        طـوب.نـقش.استفهم(هذا.نقش، 0، 0، هذا.أبعاد.عرض~مؤشر، هذا.أبعاد.طول~مؤشر)؛
        أرجع مـؤجلات.مـؤجلة[صحيح].أنشئ().{ قرر(0) }؛
    }

    عملية هذا.حرر() {
        إذا هذا.نقش != 0 طـوب.نـقش.أتلف(هذا.نقش)؛
    }

    عملية هذا.عرض: صـحيح {
        أرجع هذا.أبعاد.عرض؛
    }

    عملية هذا.طول: صـحيح {
        أرجع هذا.أبعاد.طول؛
    }

    عملية هذا.اطمغ(س: صحيح، ص: صحيح) {
        عرف مستطيل: طـوب.مـستطيل؛
        مستطيل.س = س؛
        مستطيل.ص = ص؛
        مستطيل.ع = هذا.عرض؛
        مستطيل.ط = هذا.طول؛
        طـوب.نـقش.غير_الشفافية(هذا.نقش، هذا.شفافية * 255)؛
        طـوب.رسـام.انسخ(رسام، هذا.نقش، 0، مستطيل~مؤشر)؛
    }
}

