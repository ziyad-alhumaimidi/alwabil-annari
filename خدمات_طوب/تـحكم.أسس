صنف تـحكم {
    عرف عصا: مؤشر[طـوب.عـصا_التحكم] = 0؛
    عرف حالة_أزرار_الاتجاهات: ربـاعية؛
    عرف حالة_زر_الفعل: ثنائي = 0؛
    عرف المحور_س_السابق: عائم = 0؛
    عرف المحور_ص_السابق: عائم = 0؛
    عرف زر_الفعل_السابق: ثنائي = 0؛

    عملية هذا.المحور_س: عائم {
        عرف محور_الأزرار: عائم = هذا.حالة_أزرار_الاتجاهات.يمين + هذا.حالة_أزرار_الاتجاهات.يسار؛
        إذا هذا.عصا != 0 {
            عرف محور_المقبض: عائم = هذا.هات_المحور(0)؛
            إذا محور_الأزرار * محور_الأزرار > محور_المقبض * محور_المقبض أرجع محور_الأزرار
            وإلا أرجع محور_المقبض؛
        } وإلا {
            أرجع محور_الأزرار؛
        }
    }

    عملية هذا.المحور_ص: عائم {
        عرف محور_الأزرار: عائم = هذا.حالة_أزرار_الاتجاهات.أعلى + هذا.حالة_أزرار_الاتجاهات.أسفل؛
        إذا هذا.عصا != 0 {
            عرف محور_المقبض: عائم = هذا.هات_المحور(1)؛
            إذا محور_الأزرار * محور_الأزرار > محور_المقبض * محور_المقبض أرجع محور_الأزرار
            وإلا أرجع محور_المقبض؛
        } وإلا {
            أرجع محور_الأزرار؛
        }
    }

    عملية هذا.هات_المحور(م: صحيح): عائم {
        عرف محور: صحيح = طـوب.عـصا_التحكم.هات_محور(هذا.عصا، م)؛
        إذا محور < 0 أرجع محور~مثل[عائم] ÷ 32768.0
        وإلا أرجع محور~مثل[عائم] ÷ 32767.0؛
    }

    عملية هذا.زر_الفعل: ثنائي {
        إذا هذا.عصا != 0 {
            أرجع هذا.حالة_زر_الفعل أو طـوب.عـصا_التحكم.هات_زر(هذا.عصا، 0) == 1؛
        } وإلا {
            أرجع هذا.حالة_زر_الفعل؛
        }
    }

    عملية هذا.حدث() {
        هذا.المحور_س_السابق = هذا.المحور_س؛
        هذا.المحور_ص_السابق = هذا.المحور_ص؛
        هذا.زر_الفعل_السابق = هذا.زر_الفعل؛
    }
}

