اشمل "دمـغة.أسس"؛
اشمل "مـتم/نـص.أسس"؛
اشمل "مـتم/مـصفوفة.أسس"؛

صنف كـتابة {
    عرف مـحاذاة: {
        عرف _يمين_: 1؛
        عرف _يسار_: 2؛
        عرف _وسط_: 0؛
    }

    عرف لون: لـون؛
    عرف عربي: صحيح = 0؛
    عرف محاذاة: صحيح = مـحاذاة._يسار_؛
    عرف تباعد_الأسطر: صحيح = 0؛

    عرف نص: نـص؛
    عرف سطور: مـصفوفة[سـندنا[دمـغة]]؛
    عرف س: صحيح؛
    عرف ص: صحيح؛
    عرف رقم: صحيح = 0؛

    عملية هذا.تهيئة (ل: لـون، س: صحيح، ص: صحيح) {
        هذا.حرر_السطور()؛
        هذا.نص = ""؛
        هذا.لون = ل؛
        هذا.س = س؛
        هذا.ص = ص؛
    }

    عملية هذا.عين_النص (ن: مـؤشر_محارف) {
        إذا هذا.نص == ن ارجع؛
        هذا.نص = ن؛
        هذا.هيئ_السطور()؛
    }

    عملية هذا.عين_النص(ر: صحيح) {
        إذا ر == هذا.رقم و هذا.نص != "" ارجع؛
        هذا.نص = نـص() + ر؛
        هذا.رقم = ر؛
        هذا.هيئ_السطور()؛
    }

    عملية هذا.هيئ_السطور() {
        هذا.حرر_السطور()؛
        عرف نصوص: مـصفوفة[نـص] = هذا.نص.قطع("\ج")؛
        عرف م: صحيح؛
        لكل م = 0، م < نصوص.هات_الطول()، ++م {
            عرف نقش: مؤشر[طـوب.نـقش] = طـوب.كـتابة.أنشئ_نقش_كتابة(نصوص(م).صوان، هذا.لون~مؤشر، خط_كبير، هذا.عربي، رسام)؛
            عرف دمغة: سـندنا[دمـغة]؛
            دمغة.أنشئ()؛
            دمغة.هيئ(نقش)؛
            هذا.سطور.أضف(دمغة)؛
        }
    }

    عملية هذا.حرر_السطور() {
        هذا.سطور.فرغ()؛
    }

    عملية هذا.هات_الأبعاد (): أبـعاد {
        عرف أبعاد: أبـعاد؛
        أبعاد.عرض = أبعاد.طول = 0؛
        عرف م: صحيح؛
        لكل م = 0، م < هذا.سطور.هات_الطول()، ++م {
            أبعاد.طول += هذا.سطور(م).طول؛
            إذا م != 0 أبعاد.طول += هذا.تباعد_الأسطر؛
            إذا أبعاد.عرض < هذا.سطور(م).عرض { أبعاد.عرض = هذا.سطور(م).عرض }
        }
        أرجع أبعاد؛
    }

    عملية هذا.اطمغ() {
        هذا.اطمغ(هذا.س، هذا.ص)؛
    }

    عملية هذا.اطمغ(س: صحيح، ص: صحيح) {
        عرف م: صحيح؛
        لكل م = 0، م < هذا.سطور.هات_الطول()، ++م {
            إذا هذا.محاذاة == مـحاذاة._يمين_ {
                هذا.سطور(م).اطمغ(س - هذا.سطور(م).عرض، ص)؛
            } وإلا إذا هذا.محاذاة == مـحاذاة._يسار_ {
                هذا.سطور(م).اطمغ(س، ص)؛
            } وإلا {
                هذا.سطور(م).اطمغ(س - هذا.سطور(م).عرض ÷ 2، ص)؛
            }
            ص += هذا.سطور(م).طول + هذا.تباعد_الأسطر؛
        }
    }
}

