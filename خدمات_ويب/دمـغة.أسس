صنف دمـغة {
    عرف نقش: سـندنا[مـورد_صورة]؛
    عرف أبعاد: أبـعاد؛
    عرف شفافية: عائم = 1؛

    عملية هذا~هيئ () {
    }

    عملية هذا~هيئ (سند[دمـغة]) {
        هذا.نقش = قيمة.نقش؛
        هذا.أبعاد = قيمة.أبعاد؛
        هذا.شفافية = قيمة.شفافية؛
    }؛

    عملية هذا~هيئ (ن: سـندنا[مـورد_صورة]) {
        هذا.نقش = ن؛
        هذا.أبعاد.عرض = 0؛
        هذا.أبعاد.طول = 0؛
    }

    عملية هذا=سند[دمـغة] {
        هذا.نقش = قيمة.نقش؛
        هذا.أبعاد = قيمة.أبعاد؛
        هذا.شفافية = قيمة.شفافية؛
    }

    عملية هذا.حمل(اسم_الملف: مـؤشر_محارف): سـندنا[مـؤجلات.مـؤجلة[صحيح]] {
        هذا.نقش.أنشئ()؛
        هذا.أبعاد.عرض = 0؛
        هذا.أبعاد.طول = 0؛
        أرجع هذا.نقش.حمل(اسم_الملف)؛
    }

    عملية هذا.حرر() {
        هذا.نقش.حرر()؛
    }

    عملية هذا.عرض: صـحيح {
        إذا هذا.أبعاد.عرض == 0 هذا.أبعاد = هذا.نقش.هات_الأبعاد()؛
        أرجع هذا.أبعاد.عرض؛
    }

    عملية هذا.طول: صـحيح {
        إذا هذا.أبعاد.طول == 0 هذا.أبعاد = هذا.نقش.هات_الأبعاد()؛
        أرجع هذا.أبعاد.طول؛
    }

    عملية هذا.اطمغ(س: صحيح، ص: صحيح) {
        المرسم.ارسم_صورة(هذا.نقش، س، ص، هذا.عرض، هذا.طول، هذا.شفافية)؛
    }
}

