اشمل "لاعب.أسس"؛
اشمل "عدو.أسس"؛
اشمل "طلقة.أسس"؛
اشمل "قلب.أسس"؛

// المتغيرات العمومية
//-------------------

// متغيرات الموارد
عرف نقش_الطائرة1: دمـغة؛
عرف نقش_الطائرة2: دمـغة؛
عرف نقش_الصاروخ: دمـغة؛
عرف نقش_الانفجار: دمـغة؛
عرف نقش_السماء: دمـغة؛
عرف نقش_اطلاقة: دمـغة؛
عرف نقش_صاروخ_مذهل: دمـغة؛
عرف نقش_صاروخ_سفلي: دمـغة؛
عرف نقش_خسارة: دمـغة؛
عرف نقش_قلب1: دمـغة؛
عرف نقش_قلب2: دمـغة؛
عرف نقش_إعادة_الصحة: دمـغة؛
عرف صوت: صـوت؛
عرف صوت2: صـوت؛
عرف صوت3: صـوت؛
عرف صوت4: صـوت؛
عرف صوت5: صـوت؛
عرف نقاط0: كـتابة؛
عرف نقاط1: كـتابة؛

// ثوابت
عرف عدد_الاعداء:25؛
عرف عدد_الاعداء_المذهلين:3؛
عرف عدد_الاعداء_السفليين:2؛
عرف عدد_الإطلاقات:20؛

// متغيرات اللعب
عرف ل0: لـاعب؛
عرف ل1: لـاعب؛
عرف ع:مصفوفة [عـدو، عدد_الاعداء]؛
عرف عم: مصفوفة[عـدو، عدد_الاعداء_المذهلين]؛
عرف عسس: مصفوفة[عـدو_سـفلي، عدد_الاعداء_السفليين]؛
عرف تحرك_خلفية: صحيح(0)؛
عرف تحرك_خلفية2: صحيح(1280)؛
عرف طلقة0: مصفوفة[طـلقة، عدد_الإطلاقات]؛
عرف طلقة1: مصفوفة[طـلقة، عدد_الإطلاقات]؛
عرف إعادة_الصحة: قـلب؛

// دالات التهيئة
//-------------

دالة جهز_موارد_اللعبة {
    انتظر(مـؤجلات.مـؤجلة[صحيح].الكل({
        صوت.حمل("أصوات/bullet_whizzing_by-Mike_Koenig.mp3")،
        صوت2.حمل("أصوات/إنفجارهم.mp3")،
        صوت3.حمل("أصوات/فشل.mp3")،
        صوت4.حمل("أصوات/إنفجار.mp3")،
        صوت5.حمل("أصوات/خذ_القلب.mp3")،
        نقش_الطائرة1.حمل("رسوم/طائرة.png")،
        نقش_الطائرة2.حمل("رسوم/طائرة2.png")،
        نقش_الصاروخ.حمل("رسوم/صاروخ.png")،
        نقش_الانفجار.حمل("رسوم/انفجار.png")،
        نقش_السماء.حمل("رسوم/سماء.png")،
        نقش_اطلاقة.حمل("رسوم/طلقة.png")،
        نقش_صاروخ_مذهل.حمل("رسوم/صاروخ_مذهل.png")،
        نقش_صاروخ_سفلي.حمل("رسوم/قنبلة.png")،
        نقش_خسارة.حمل("رسوم/خسارة.png")،
        نقش_قلب1.حمل("رسوم/قلب.png")،
        نقش_قلب2.حمل("رسوم/قلب2.png")،
        نقش_إعادة_الصحة.حمل("رسوم/إعادة_الصحة.png")،
    }).تجاهل_النتيجة())؛

    عرف لون: لـون(0، 0، 0، 255)؛
    نقاط0.تهيئة(لون، 60، 15)؛
    لون.أحمر = 100؛ لون.أخضر = 100؛ لون.أزرق = 100؛
    نقاط1.تهيئة(لون، 60، 690)؛
}

دالة هيئ_اللعبة {
  ل0.ميت =0؛
  ل0.ميت_السابق = 0؛
  ل0.ص = 250؛
  ل0.س = 0؛
  ل0.عداد_الانفجار = 0؛
  ل0.صحة=0؛
  ل0.عداد_الإطلاق = 0؛
  ل0.نقاط = 0؛

  ل1.ميت =0؛
  ل1.ميت_السابق = 0؛
  ل1.ص = 250؛
  ل1.س = 0؛
  ل1.عداد_الانفجار = 0؛
  ل1.صحة=0؛
  ل1.عداد_الإطلاق = 0؛
  ل1.نقاط = 0؛

  عرف ه: صحيح؛

  لكل ه=0، ه < عدد_الاعداء، ه+=1{
    ع(ه).س = مـتم.ريـاضيات.عشوائي() % 1280 + 1000؛
    ع(ه).ص = مـتم.ريـاضيات.عشوائي() % 700؛
    ع(ه).سس = مـتم.ريـاضيات.عشوائي() % 10+5؛
    ع(ه).سص = مـتم.ريـاضيات.عشوائي() % 10-5؛
    ع(ه).طاقة=0؛
  }؛

  لكل ه=0، ه < عدد_الاعداء_المذهلين، ه+=1{
    عم(ه).س = مـتم.ريـاضيات.عشوائي() % 1280 + 1000؛
    عم(ه).ص = مـتم.ريـاضيات.عشوائي() % 700؛
    عم(ه).سس = مـتم.ريـاضيات.عشوائي() % 10+5؛
    عم(ه).سص = مـتم.ريـاضيات.عشوائي() % 10-5؛
    عم(ه).طاقة=0؛
  }؛

  لكل ه=0، ه < عدد_الاعداء_السفليين، ه+=1{
    عسس(ه).س = مـتم.ريـاضيات.عشوائي() % 1280؛
    عسس(ه).ص = مـتم.ريـاضيات.عشوائي() % 700؛
    عسس(ه).سس = مـتم.ريـاضيات.عشوائي() % 10-5؛
    عسس(ه).سص = 3؛
  }؛

  لكل ه=0، ه < عدد_الإطلاقات، ه+=1{
    طلقة0(ه).ميت = 1؛
    طلقة1(ه).ميت = 1؛
  }؛

  إعادة_الصحة.ميت=0؛
}؛

// دالات اللعب
//-----------

دالة تحسس_اطلاقة(ط:سند[طـلقة]، ع:سند[عـدو]، ل: سند[لـاعب]) {
  إذا ع.ميت == 1 أو ط.ميت == 1 ارجع؛

  عرف طس1: صحيح = ط.س+2؛
  عرف طس2: صحيح = ط.س+11؛
  عرف عس1: صحيح = ع.س+5؛
  عرف عس2: صحيح = ع.س+100؛
  عرف طص1: صحيح = ط.ص+3؛
  عرف طص2: صحيح = ط.ص+7؛
  عرف عص1: صحيح = ع.ص+12؛
  عرف عص2: صحيح = ع.ص+53؛

  إذا طس1 < عس2 و طس2 > عس1  {
    إذا طص1 < عص2 و طص2 > عص1{
      ع.طاقة +=1؛
      ط.ميت=1؛
      إذا ع.طاقة ==3{
        ع.ميت=1؛
        صوت2.شغل()؛
        ع.طاقة=0؛
        ل.نقاط += 5؛
      }؛
    }؛
  }؛
}؛

دالة تحسس(ل:سند[لـاعب]، ع:سند [عـدو]) {
  عرف لس1: صحيح = ل.س؛
  عرف لس2: صحيح = ل.س+35؛
  عرف عس1: صحيح = ع.س+5؛
  عرف عس2: صحيح = ع.س+100؛
  عرف لص1: صحيح = ل.ص+10؛
  عرف لص2: صحيح = ل.ص+35؛
  عرف عص1: صحيح = ع.ص+12؛
  عرف عص2: صحيح = ع.ص+53؛

  إذا لس1 < عس2 و لس2 > عس1  {
    إذا لص1 < عص2 و لص2 > عص1{
      إذا ع.ميت ==0{
        إذا ل.ميت ==0{
          ل.صحة+=1؛
          ع.ميت=1؛
          صوت2.شغل()؛
          إذا ل.صحة>=3{
            ل.ميت=1؛
            ع.ميت=1؛
          }؛
        }؛
      }؛
    }؛
  }؛
}؛

دالة تحسس_قلب(ل:سند[لـاعب]، ق:سند [قـلب]) {
  عرف لس1: صحيح = ل.س؛
  عرف لس2: صحيح = ل.س+35؛
  عرف قس1: صحيح = ق.س+5؛
  عرف قس2: صحيح = ق.س+100؛
  عرف لص1: صحيح = ل.ص+10؛
  عرف لص2: صحيح = ل.ص+35؛
  عرف قص1: صحيح = ق.ص+12؛
  عرف قص2: صحيح = ق.ص+100؛

  إذا لس1 < قس2 و لس2 > قس1  {
    إذا لص1 < قص2 و لص2 > قص1{
      إذا ق.ميت ==0{
        إذا ل.ميت ==0{
          إذا ل.صحة > 0 {
            ل.صحة=0؛
            ق.ميت=1؛
            صوت5.شغل()؛
          }؛
        }؛
      }؛
    }؛
  }؛
}؛

دالة تحسس_مذهل(ل:سند[لـاعب]، ع:سند [عـدو]) {
  عرف لس1: صحيح = ل.س؛
  عرف لس2: صحيح = ل.س+35؛
  عرف عس1: صحيح = ع.س+5؛
  عرف عس2: صحيح = ع.س+100؛
  عرف لص1: صحيح = ل.ص+10؛
  عرف لص2: صحيح = ل.ص+35؛
  عرف عص1: صحيح = ع.ص+12؛
  عرف عص2: صحيح = ع.ص+102؛

  إذا لس1 < عس2 و لس2 > عس1  {
    إذا لص1 < عص2 و لص2 > عص1{
      إذا ع.ميت ==0{
        إذا ل.ميت ==0{
          ل.صحة+=1؛
          إذا ل.صحة>=3{
            ل.ميت=1؛
          }؛
        }؛
      }؛
    }؛
  }؛
}؛

دالة تحسس_اطلاقة_للمذهل(ط:سند[طـلقة]، ع:سند[عـدو]، ل: سند[لـاعب]) {
  إذا ط.ميت==0{
    عرف طس1: صحيح = ط.س+2؛
    عرف طس2: صحيح = ط.س+11؛
    عرف عس1: صحيح = ع.س+5؛
    عرف عس2: صحيح = ع.س+100؛
    عرف طص1: صحيح = ط.ص+3؛
    عرف طص2: صحيح = ط.ص+7؛
    عرف عص1: صحيح = ع.ص+12؛
    عرف عص2: صحيح = ع.ص+102؛

    إذا طس1 < عس2 و طس2 > عس1  {
      إذا طص1 < عص2 و طص2 > عص1{
        ع.طاقة +=1؛
        ط.ميت=1؛
        إذا ع.طاقة ==10{
         ع.ميت=1؛
         ل.نقاط += 25؛
         ع.طاقة=0؛
        }؛
      }؛
    }؛
  }؛
}؛

دالة تحسس_اطلاقة_للسفلي(ط:سند[طـلقة]، ع:سند[عـدو_سـفلي]) {
  إذا ط.ميت==0{
    عرف طس1: صحيح = ط.س+2؛
    عرف طس2: صحيح = ط.س+11؛
    عرف عس1: صحيح = ع.س+5؛
    عرف عس2: صحيح = ع.س+100؛
    عرف طص1: صحيح = ط.ص+3؛
    عرف طص2: صحيح = ط.ص+7؛
    عرف عص1: صحيح = ع.ص+12؛
    عرف عص2: صحيح = ع.ص+75؛

    إذا طس1 < عس2 و طس2 > عس1 {
      إذا طص1 < عص2 و طص2 > عص1 {
        ط.ميت=1؛
      }؛
    }؛
  }؛
}؛

دالة تحسس_سفلي(ل:سند[لـاعب]، ع:سند [عـدو_سـفلي]) {
  عرف لس1: صحيح = ل.س؛
  عرف لس2: صحيح = ل.س+35؛
  عرف عس1: صحيح = ع.س+5؛
  عرف عس2: صحيح = ع.س+100؛
  عرف لص1: صحيح = ل.ص+10؛
  عرف لص2: صحيح = ل.ص+35؛
  عرف عص1: صحيح = ع.ص+12؛
  عرف عص2: صحيح = ع.ص+75؛

  إذا لس1 < عس2 و لس2 > عس1  {
    إذا لص1 < عص2 و لص2 > عص1{
      إذا ع.ميت ==0{
        إذا ل.ميت ==0{
          ل.صحة+=1؛
          إذا ل.صحة>=3{
            ل.ميت=1؛
            ع.ميت=1؛
          }؛
        }؛
      }؛
    }؛
  }؛
}؛

دالة اللعبة {
  عرف عداد_نهاية_اللعبة: صحيح = 0؛
  بينما 1 {
    // تحديث ورسم الخلفية
    نقش_السماء.اطمغ(تحرك_خلفية، 0)؛
    نقش_السماء.اطمغ(تحرك_خلفية2، 0)؛
    تحرك_خلفية-=1؛
    تحرك_خلفية2-=1؛
    إذا تحرك_خلفية == -1280{
      تحرك_خلفية = 1280؛
    }؛
    إذا تحرك_خلفية2 == -1280{
    تحرك_خلفية2 = 1280؛
    }؛

    عرف ه: صحيح؛

    // تحديث ورسم الأعداء
    لكل ه=0، ه<عدد_الاعداء، ه=ه+1{
     إذا ع(ه).ميت == 0{
      نقش_الصاروخ.اطمغ(ع(ه).س، ع(ه).ص)؛
      حرك_العدو(ع(ه))؛
      تحسس(ل0،ع(ه))؛
      تحسس(ل1،ع(ه))؛
      عرف ههه: صحيح؛
      لكل ههه = 0، ههه<عدد_الإطلاقات، ههه += 1 {
        تحسس_اطلاقة(طلقة0(ههه)، ع(ه)، ل0)؛
        تحسس_اطلاقة(طلقة1(ههه)، ع(ه)، ل1)؛
      }
     } وإلا {
       إذا ع(ه).عداد_الانفجار < 200{
          نقش_الانفجار.اطمغ(ع(ه).س، ع(ه).ص)؛
          ع(ه).عداد_الانفجار +=1
       } وإلا {
          ع(ه).عداد_الانفجار = 0؛
          ع(ه).ميت = 0؛
          ع(ه).س = 1280
       }؛
     }؛
    }؛
    لكل ه=0، ه<عدد_الاعداء_المذهلين، ه=ه+1{
     إذا عم(ه).ميت == 0{
      نقش_صاروخ_مذهل.اطمغ(عم(ه).س، عم(ه).ص)؛
      حرك_العدو(عم(ه))؛
      تحسس_مذهل(ل0،عم(ه))؛
      تحسس_مذهل(ل1،عم(ه))؛
      عرف هه :صحيح؛
      لكل هه = 0، هه<عدد_الإطلاقات، هه += 1 {
      تحسس_اطلاقة_للمذهل(طلقة0(هه)، عم(ه)، ل0)؛
      تحسس_اطلاقة_للمذهل(طلقة1(هه)، عم(ه)، ل1)؛
     }
     } وإلا{
       إذا عم(ه).عداد_الانفجار < 200{
          نقش_الانفجار.اطمغ(عم(ه).س، عم(ه).ص)؛
          عم(ه).عداد_الانفجار +=1
       } وإلا {
          عم(ه).عداد_الانفجار = 0؛
          عم(ه).ميت = 0؛
          عم(ه).طاقة=0؛
          عم(ه).س = 1280؛
         }؛
       }؛
    }؛

    لكل ه=0، ه<عدد_الاعداء_السفليين، ه=ه+1{
     إذا عسس(ه).ميت == 0{
      نقش_صاروخ_سفلي.اطمغ(عسس(ه).س، عسس(ه).ص)؛
      حرك_العدو_السفلي(عسس(ه))؛
      تحسس_سفلي(ل0،عسس(ه))؛
      تحسس_سفلي(ل1،عسس(ه))؛
      عرف هههه:صحيح؛
      لكل هههه = 0، هههه<عدد_الإطلاقات، هههه += 1 {
      تحسس_اطلاقة_للسفلي(طلقة0(هههه)، عسس(ه))؛
      تحسس_اطلاقة_للسفلي(طلقة1(هههه)، عسس(ه))؛
     }
     } وإلا{
       إذا عسس(ه).عداد_الانفجار < 200{
          نقش_الانفجار.اطمغ(عسس(ه).س، عسس(ه).ص)؛
          عسس(ه).عداد_الانفجار +=1
       } وإلا {
          عسس(ه).عداد_الانفجار = 0؛
          عسس(ه).ميت = 0؛
          عسس(ه).ص = 720؛
          عسس(ه).سص = 3؛
         }؛
       }؛
    }؛

    تحسس_قلب(ل0،إعادة_الصحة)؛
    تحسس_قلب(ل1،إعادة_الصحة)؛

    إذا ل0.ميت ==1 و ل1.ميت ==1 {
      ++عداد_نهاية_اللعبة؛
      إذا عداد_نهاية_اللعبة == 160 {
        صوت3.شغل()؛
        نقش_خسارة.اطمغ(0، 0)؛
        لكل ه=0، ه<150، ه+=1{
          مـتم.نـظام.نم(15000)؛
          عالج_الأحداث()؛
        }؛
        ارجع
      }
    }

    // تحديث اللاعبين
    عرف ب : صحيح = 0؛
    عرف بب : صحيح = 0؛
    حدث_اللاعب(نقش_الطائرة1، ل0، تحكم1، نقش_قلب1، بب)؛
    ب=55؛
    بب=585؛
    حدث_اللاعب(نقش_الطائرة2،ل1، تحكم2، نقش_قلب2، بب)؛

    // تحديث الطلقة
    لكل ه=0، ه<عدد_الإطلاقات، ه+=1{
    حدث_الطلقة(طلقة0(ه)، ل0، تحكم1)؛
    حدث_الطلقة(طلقة1(ه)، ل1، تحكم2)؛
    }؛
    حدث_القلب(نقش_إعادة_الصحة، إعادة_الصحة)؛

    نقاط0.عين_النص(ل0.نقاط)؛
    نقاط0.اطمغ()؛
    إذا زوجي == 1 {
      نقاط1.عين_النص(ل1.نقاط)؛
      نقاط1.اطمغ()؛
    }؛

    تحكم1.حدث()؛
    تحكم2.حدث()؛
    مـتم.نـظام.نم(15000)؛
    عالج_الأحداث()؛
  }؛
}؛

