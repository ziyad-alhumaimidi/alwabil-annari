اشمل "مـتم/ريـاضيات"؛
اشمل "مـتم/سندات"؛
اشمل "مـتم/طـرفية"؛
اشمل "مـتم/نـظام"؛
اشمل "مـتم/نـص"؛
اشمل "مـتم/نـم"؛
اشمل "مـحا.أسس"؛
مـحا.اشمل_ملف("Alusus/Sdl2"، "طـوب.أسس")؛
مـحا.اشمل_ملف("Alusus/Promises"، "مـؤجلات.أسس")؛

اشمل "خدمات_طوب/تـحكم.أسس"؛
اشمل "خدمات_طوب/دمـغة.أسس"؛
اشمل "خدمات_طوب/كـتابة.أسس"؛
اشمل "خدمات_طوب/صـوت.أسس"؛
اشمل "خدمات_طوب/مـوسيقى.أسس"؛

اشمل "القائمة.أسس"؛

استخدم مـتم؛

//=============================================================================
// متغيرات عمومية

عرف رسام: مؤشر[طـوب.رسـام]؛
عرف خط_كبير: مؤشر[طـوب.كـتابة.خـط]؛

//=============================================================================
// أصناف

صنف ربـاعية {
    عرف أعلى: عائم = 0؛
    عرف يمين: عائم = 0؛
    عرف أسفل: عائم = 0؛
    عرف يسار: عائم = 0؛
}

صنف أبـعاد {
    عرف عرض: صحيح = 0؛
    عرف طول: صحيح = 0؛
    عملية هذا~هيئ() {}
    عملية هذا~هيئ(سند_مؤقت[أبـعاد]) {
        هذا = قيمة؛
    }
    عملية هذا = سند_مؤقت[أبـعاد] {
        هذا.عرض = قيمة.عرض؛
        هذا.طول = قيمة.طول؛
    }
}

عرف لـون: لقب طـوب.لـون؛
@دمج صنف لـون {
    عرف أحمر: لقب ح؛
    عرف أخضر: لقب خ؛
    عرف أزرق: لقب ز؛
}

//=============================================================================
// دالات

@تصدير[exit] دالة اخرج (حالة: صحيح);

دالة اطمغ (نقش: مؤشر[طـوب.نـقش]، س: صحيح، ص: صحيح) {
  عرف مستطيل: طـوب.مـستطيل؛
  مستطيل.س = س؛
  مستطيل.ص = ص؛
  طـوب.نـقش.استفهم(نقش، 0، 0، مستطيل.ع~مؤشر، مستطيل.ط~مؤشر)؛
  طـوب.رسـام.انسخ(رسام، نقش، 0، مستطيل~مؤشر)؛
}؛

دالة عالج_الاحداث {
  طـوب.عـصا_التحكم.حدث()؛
  عرف حدث: طـوب.حـدث؛
  بينما طـوب.استشعر_حدث(حدث~مؤشر) != 0 {
    إذا حدث.نوع == طـوب.حـدث._خروج_ اخرج(0)؛
  }؛
  طـوب.رسـام.اعرض(رسام)؛
}؛

دالة انتظر [صـنف_البيانات: صنف = صـحيح] (م: سـندنا[مـؤجلات.مـؤجلة[صـنف_البيانات]]): صـنف_البيانات {
    بينما 1 {
        إذا م.الحالة != مـؤجلات.حـالة._جديد_ اقطع؛
        مـتم.نـظام.نم(15000)؛
        عالج_الاحداث()؛
    }
    أرجع م.النتيجة؛
}

//=============================================================================
// تهيئات

إذا طـوب.هيئ(طـوب._تهيئة_عصا_التحكم_) != 0 {
  مـتم.طـرفية.اطبع("فشلت تهئية مكتبة ط.و.ب.\ج")؛
  اخرج(1)؛
}؛

إذا طـوب.صـورة.هيئ(طـوب.صـورة._تهيئة_بنج_) != طـوب.صـورة._تهيئة_بنج_ {
  مـتم.طـرفية.اطبع("فشلت تهيئة مكتبة صورة ط.و.ب\ج")؛
  اخرج(1)؛
}؛

إذا طـوب.سـمعي.هيئ(44100، طـوب.سـمعي.بـنية._مبدئي_، 2، 2048) < 0 {
  مـتم.طـرفية.اطبع("فشلت تهيئة مكتبة السمعيات\ج")؛
  اخرج(1)؛
}؛
طـوب.كـتابة.هيئ()؛
خط_كبير = طـوب.كـتابة.افتح_خط("رسوم/AlususMono.otf"، 40)؛
  إذا خط_كبير == 0 {
    مـتم.طـرفية.اطبع("فشل تحميل الخط.\ج")؛
    اخرج(1)؛
  }؛


عرف نافذة: مؤشر[طـوب.نـافذة] = طـوب.نـافذة.أنشئ("الوابل الناري"، 0، 0، 1280، 720، 0)؛
إذا نافذة == 0 {
  مـتم.طـرفية.اطبع("فشل إنشاء النافذة\ج")؛
  اخرج(1)؛
}؛

طـوب.عين_تلميح(طـوب.تـلميحات._رسوم_جودة_التحجيم_، "linear")؛

رسام = طـوب.رسـام.أنشئ(نافذة، -1، طـوب.رسـام.عـلامات._مسرع_)؛
إذا رسام == 0 {
  مـتم.طـرفية.اطبع("فشل إنشاء الرسام\ج")؛
  اخرج(1)؛
}؛

إذا طـوب.هيئ(طـوب._تهيئة_عصا_التحكم_) == 0 {
    إذا طـوب.عـصا_التحكم.هات_عددها() >= 2 {
      تحكم1.عصا= طـوب.عـصا_التحكم.افتح(0)؛
      تحكم2.عصا = طـوب.عـصا_التحكم.افتح(1)؛
      مـتم.طـرفية.اطبع("تم تهيئة عصا التحكم\ج")؛
    } وإلا {
      إذا طـوب.عـصا_التحكم.هات_عددها() >= 1 {
        تحكم1.عصا = طـوب.عـصا_التحكم.افتح(0)؛
        مـتم.طـرفية.اطبع("تم تهيئة عصا التحكم\ج")؛
      } وإلا {
        مـتم.طـرفية.اطبع("لم نجد عصا تحكم\ج")؛
        اخرج(1)؛
      }؛
    }؛
} وإلا {
    مـتم.طـرفية.اطبع("فشل تهيئة مكتبة الوسائط\ج")؛
    اخرج(1)؛
}؛

//=============================================================================
// البداية

جهز_الموارد()؛
شغل_الموسيقى()؛
القائمة()؛

